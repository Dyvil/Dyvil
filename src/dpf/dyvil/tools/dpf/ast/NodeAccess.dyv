package dyvil.tools.dpf.ast

import dyvil.collection.Map
import dyvil.tools.dpf.visitor.NodeVisitor
import dyvil.tools.dpf.visitor.ValueVisitor
import dyvil.tools.parsing.Name
import dyvil.tools.parsing.ast.IASTNode
import dyvil.tools.parsing.position.ICodePosition

public class NodeAccess implements NodeVisitor, NodeElement, Expandable
{
	protected Name name = null
	{
		public get
	}

	protected NodeElement element
	
	private ICodePosition position
	
	public init(Name name)
	{
		this.name = name
	}

	public init(Name name, ICodePosition position)
	{
		this.name = name
		this.position = position
	}

	public override func getPosition(): ICodePosition = this.position

	public override func setPosition(ICodePosition position): void = this.position = position

	public override func visitNode(Name name): NodeVisitor
	{
		Node node = new Node(name)
		this.element = node
		return node
	}
	
	public override func visitProperty(Name name): ValueVisitor
	{
		Property property = new Property(name)
		this.element = property
		return property
	}
	
	public override func visitNodeAccess(Name name): NodeVisitor
	{
		NodeAccess access = new NodeAccess(name)
		this.element = access
		return access
	}
	
	public override func accept(NodeVisitor visitor): void
	{
		this.element.accept(visitor.visitNodeAccess(this.name))
	}

	public override func expand(Map<String, Object> mappings, boolean mutate): NodeAccess
	{
		NodeAccess nodeAccess = mutate ? this : new NodeAccess(this.name, this.position)
		nodeAccess.element = Expandable.expand(this.element, mappings, mutate) as NodeElement
		return nodeAccess
	}

	public override func toString(): String = IASTNode.toString(this)
	
	public override func toString(String indent, StringBuilder buffer): void
	{
		buffer.append(this.name).append('.')
		this.element.toString(indent, buffer)
	}

	public override func equals(Object obj): boolean
	{
		if (obj === this) return true
		if (!(obj is NodeAccess)) return false

		let that = obj as NodeAccess
		if (this.name !== that.name) return false
		if (this.element != that.element) return false
		return true
	}

	public override func hashCode(): int = 31 * this.name.hashCode + this.element.hashCode
}
